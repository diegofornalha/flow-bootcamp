# 

### ü§ëCrie e fa√ßa upload de nossos ativos NFT

Todo esse tempo escrevemos c√≥digo. Vamos mudar de marcha e criar id√©ias da NFT!

√â aqui que voc√™ decide sobre o que ser√° sua cole√ß√£o NFT. √â sobre seus personagens favoritos de anime? Talvez alguns p√¥steres legais de filmes? Voc√™ tamb√©m pode colocar gatos na blockchain pela en√©sima vez. √â exatamente o que eu vou fazer¬†üòπ

**Agora, √© a hora de voc√™ ser insanamente criativo. Crie cinco NFTs aleat√≥rias para sua cole√ß√£o.**¬†Suas NFTs podem ser v√≠deos, imagens ou at√© arquivos mp3! Vou usar imagens antigas simples no formato .png.

Crie uma pasta chamada "ativos" e adicione seus arquivos nela. Verifique se eles s√£o nomeados sequencialmente de 0 a 5 ( ou mais ). N√≥s vamos armazen√°-los¬†[IPFS](https://en.wikipedia.org/wiki/InterPlanetary_File_System?utm_source=buildspace.so&utm_medium=buildspace_project), que √© essencialmente um sistema de arquivos distribu√≠dos. Hoje ‚Äî voc√™ pode usar algo como S3, GCP Storage ou Dropbox. Mas, nesse caso, podemos simplesmente confiar no IPFS, administrado por estranhos que est√£o usando a rede. D√™¬†[isto](https://decrypt.co/resources/how-to-use-ipfs-the-backbone-of-web3?utm_source=buildspace.so&utm_medium=buildspace_project)¬†uma leitura r√°pida quando puder! Abrange muitos bons conhecimentos b√°sicos: ).

Usaremos um servi√ßo gratuito chamado¬†[NFT.Storage](https://nft.storage/)¬†para carregar nossos ativos no IPFS. Fa√ßa uma conta e fa√ßa login. Usaremos o aplicativo de desktop NFTUp para fazer upload de arquivos. A instala√ß√£o levar√° cerca de 60 segundos, apenas¬†[siga as instru√ß√µes aqui](https://nft.storage/docs/how-to/nftup/).

Arraste + solte seus arquivos ou clique no bot√£o de upload e selecione-os.

[https://camo.githubusercontent.com/76ec1460e53ee9f03aa7497eadd08d60dcd14f96d7a10e4da8a4d580210e3114/68747470733a2f2f6861636b6d642e696f2f5f75706c6f6164732f7231646b6a462d63632e706e67](https://camo.githubusercontent.com/76ec1460e53ee9f03aa7497eadd08d60dcd14f96d7a10e4da8a4d580210e3114/68747470733a2f2f6861636b6d642e696f2f5f75706c6f6164732f7231646b6a462d63632e706e67)

Isso fornecer√° tr√™s coisas - o CID ( ID do conte√∫do ), o URL do IPFS e o URL do Gateway. O URL do IPFS √© o que usaremos em nosso contrato.

[https://camo.githubusercontent.com/54340425a78114e5ba20581391157ee5177bd8166abb64bd650f8c78d80cdd8e/68747470733a2f2f6861636b6d642e696f2f5f75706c6f6164732f484a383859745771352e706e67](https://camo.githubusercontent.com/54340425a78114e5ba20581391157ee5177bd8166abb64bd650f8c78d80cdd8e/68747470733a2f2f6861636b6d642e696f2f5f75706c6f6164732f484a383859745771352e706e67)

Voc√™ pode clicar no URL do gateway para ver suas NFTs no navegador. A parte legal aqui √© que isso se comporta como uma unidade de armazenamento em nuvem com seus ativos. Voc√™ pode acessar todos os seus arquivos com o mesmo link, apenas substituindo o nome do arquivo no final.

[https://camo.githubusercontent.com/b4ba6eec43ad848347643f31dbc380927a9e0e198614b1db75249409c7bf0a25/68747470733a2f2f6861636b6d642e696f2f5f75706c6f6164732f534a687a61595a63632e706e67](https://camo.githubusercontent.com/b4ba6eec43ad848347643f31dbc380927a9e0e198614b1db75249409c7bf0a25/68747470733a2f2f6861636b6d642e696f2f5f75706c6f6164732f534a687a61595a63632e706e67)

Nossos ativos est√£o prontos para partir! Esses ativos s√£o permanentes - eles nunca caem ou mudam, ent√£o voc√™ pode ter certeza de que suas NFTs sobreviver√£o¬†*qualquer coisa!*

**Nota**¬†Voc√™¬†*pode*¬†use imagens na Internet regularmente de sites como Imgur, mas esses links podem quebrar e n√£o s√£o recomendados para NFTs.

Se voc√™ decidir usar sua pr√≥pria imagem, verifique se o URL vai diretamente para a imagem real, n√£o para o site que hospeda a imagem! Por exemplo, links diretos da Imgur s√£o assim -¬†`https://i.imgur.com/123123.png`¬†N√ÉO¬†`https://imgur.com/gallery/123123`. A maneira mais f√°cil de dizer √© verificar se o URL termina em uma extens√£o de imagem como .png ou .jpg. Voc√™ pode clicar com o bot√£o direito do mouse na imagem do imgur e em "copiar endere√ßo da imagem". Isso fornecer√° o URL correto.

Nosso contrato √© muito bom agora. √â bom o suficiente para podermos implant√°-lo na rede de teste. Mas √© apenas mais um contrato agora. N√£o h√° nada que diga a todos os aplicativos em execu√ß√£o no Flow que √© um contrato da NFT.

Se o OpenSea decidisse que o BottomShot era a pr√≥xima grande novidade e que eles queriam listar as NFTs, eles teriam que revisar o contrato e examinar todas as fun√ß√µes e criar em torno dele. Isso √© meio coxo, devemos seguir o mesmo padr√£o que outros projetos por a√≠! Nosso contrato n√£o declara explicitamente que atende a quaisquer padr√µes da NFT. Vamos consertar isso!

### üéüO padr√£o NonFungibleToken

O ecossistema Flow tem um padr√£o para as NFTs com as quais todos concordaram. A equipe Flow o implantou no¬†[rede de teste e rede principal](https://docs.onflow.org/core-contracts/non-fungible-token/), portanto, quando voc√™ chegar a essas redes, poder√° importar apenas do endere√ßo existente. Por enquanto, vamos implant√°-lo em nosso emulador.

N√≥s vamos importar um¬†*interface*¬†para tokens n√£o fung√≠veis que implementa o padr√£o para todas as NFTs no fluxo. Se voc√™ vem de Ethereum, √© como o ERC721 do Flow.

Cabe√ßa¬†[aqui](https://github.com/onflow/flow-nft/blob/master/contracts/NonFungibleToken.cdc)¬†para o c√≥digo. Fa√ßa um novo arquivo chamado¬†`NonFungibleToken.cdc`¬†na pasta de contratos e adicione o c√≥digo.

Depois de copiado, clique no bot√£o "implantar" na parte superior do arquivo NonFungibleToken.cdc. Isso implantar√° essa interface de contrato na conta de servi√ßo padr√£o -¬†`0xf8d6e0586b0a20c7`

Volte ao arquivo do contrato para poder import√°-lo na parte superior e adicionar v√°rias outras altera√ß√µes:

`// We're importing the contract from the default service account
import NonFungibleToken from 0xf8d6e0586b0a20c7;

// Here we tell Cadence that our BottomShot contract implements the interface
pub contract BottomShot: NonFungibleToken {

  pub var totalSupply: UInt64

  pub event ContractInitialized()
  pub event Withdraw(id: UInt64, from: Address?)
  pub event Deposit(id: UInt64, to: Address?)

  pub let CollectionStoragePath: StoragePath
  pub let CollectionPublicPath: PublicPath

  // Our NFT resource conforms to the INFT interface
  pub resource NFT: NonFungibleToken.INFT {
    pub let id: UInt64

    pub let name: String
    pub let description: String
    pub let thumbnail: String

    init(
        id: UInt64,
        name: String,
        description: String,
        thumbnail: String,
    ) {
        self.id = id
        self.name = name
        self.description = description
        self.thumbnail = thumbnail
    }
  }

  pub resource interface BottomShotCollectionPublic {
    pub fun deposit(token: @NonFungibleToken.NFT)
    pub fun getIDs(): [UInt64]
    pub fun borrowNFT(id: UInt64): &NonFungibleToken.NFT
  }

  // Same goes for our Collection, it conforms to multiple interfaces 
  pub resource Collection: BottomShotCollectionPublic, NonFungibleToken.Provider, NonFungibleToken.Receiver, NonFungibleToken.CollectionPublic {
    pub var ownedNFTs: @{UInt64: NonFungibleToken.NFT}

    init () {
      self.ownedNFTs <- {}
    }
    
    // An upgraded deposit function
    pub fun deposit(token: @NonFungibleToken.NFT) {
      let token <- token as! @BottomShot.NFT

      let id: UInt64 = token.id

      // Add the new token to the dictionary, this removes the old one
      let oldToken <- self.ownedNFTs[id] <- token
      
      // Trigger an event to let listeners know an NFT was deposited to this collection
      emit Deposit(id: id, to: self.owner?.address)
      
      // Destroy (burn) the old NFT
      destroy oldToken
    }

    pub fun withdraw(withdrawID: UInt64): @NonFungibleToken.NFT {
      let token <- self.ownedNFTs.remove(key: withdrawID) ??
        panic("This collection doesn't contain an NFT with that ID")

      emit Withdraw(id: token.id, from: self.owner?.address)

      return <-token
    }

    // getIDs returns an array of the IDs that are in the collection
    pub fun getIDs(): [UInt64] {
      return self.ownedNFTs.keys
    }

    pub fun borrowNFT(id: UInt64): &NonFungibleToken.NFT {
      return (&self.ownedNFTs[id] as &NonFungibleToken.NFT?)!
    }

    destroy() {
      destroy self.ownedNFTs
    }
  }

  pub fun createEmptyCollection(): @NonFungibleToken.Collection {
    return <- create Collection()
  }

  // Mints a new NFT with a new ID and deposits it 
  // in the recipients collection using their collection reference
  pub fun mintNFT(
    recipient: &{NonFungibleToken.CollectionPublic},
    name: String,
    description: String,
    thumbnail: String,
  ) {
    // create a new NFT
    var newNFT <- create NFT(
      id: BottomShot.totalSupply,
      name: name,
      description: description,
      thumbnail: thumbnail
    )

    // Deposit it in the recipient's account using their collection ref
    recipient.deposit(token: <-newNFT)

    BottomShot.totalSupply = BottomShot.totalSupply + UInt64(1)
  }

  init() {
    self.totalSupply = 0

    self.CollectionStoragePath = /storage/BottomShotCollection
    self.CollectionPublicPath = /public/BottomShotCollection

    // Create a Collection for the deployer
    let collection <- create Collection()
    self.account.save(<-collection, to: self.CollectionStoragePath)

    // Create a public capability for the collection
    self.account.link<&BottomShot.Collection{NonFungibleToken.CollectionPublic, BottomShot.BottomShotCollectionPublic}>(
      self.CollectionPublicPath,
      target: self.CollectionStoragePath
    )

    emit ContractInitialized()
  }
}`

Se a extens√£o Cadence gritar com voc√™, reinicie o servidor na paleta de comandos (`CMD`/`CTRL`+`SHIFT`+`P`):

[https://camo.githubusercontent.com/6ffe413548d41c95c785708d28f73afd876d349e6db6ea26df0265425dfb02e0/68747470733a2f2f6861636b6d642e696f2f5f75706c6f6164732f726b444f636e4f59712e706e67](https://camo.githubusercontent.com/6ffe413548d41c95c785708d28f73afd876d349e6db6ea26df0265425dfb02e0/68747470733a2f2f6861636b6d642e696f2f5f75706c6f6164732f726b444f636e4f59712e706e67)

Woah. Parece muito c√≥digo. N√£o se preocupe, voc√™ sabe a maior parte! Al√©m disso, a Cadence torna as novas pe√ßas muito simples. Vamos dividir linha por linha.

`import NonFungibleToken from 0xf8d6e0586b0a20c7;

pub contract BottomShot: NonFungibleToken {`

Como mencionado anteriormente, estamos importando a interface NonFungibleToken da conta para a qual foi implantada. N√≥s o usamos ao declarar nosso contrato, informando ao compilador que nosso contrato atende aos requisitos da interface.

O que √© uma interface? √â um tipo abstrato que define o comportamento das coisas que o implementam. Ainda confuso? Eu tamb√©m lol.

Voc√™ pode pensar em uma interface como um conjunto de regras ou uma descri√ß√£o. Imagine a interface para um sedan: 4 portas, um corpo fechado ( sem teto aberto ) e uma se√ß√£o separada para o motorista e os passageiros. Se eu lhe mostrasse um BMW chique de 2 portas sem teto, voc√™ me diria que n√£o √© um sedan! Da mesma forma, as interfaces de contrato nos ajudam a definir e definir padr√µes. Se eu declarar que meu contrato atende √† interface NFT, mas n√£o implemente todos os requisitos, ele n√£o ser√° executado.

  `pub var totalSupply: UInt64

  pub event ContractInitialized()
  pub event Withdraw(id: UInt64, from: Address?)
  pub event Deposit(id: UInt64, to: Address?)
  
  pub let CollectionStoragePath: StoragePath
  pub let CollectionPublicPath: PublicPath`

Em seguida, temos v√°rias novas vari√°veis. J√° sabemos¬†`totalSupply`.

O¬†`event`¬†type is new - √© basicamente um webhook no blockchain. Podemos acionar / emitir esses eventos em determinados lugares para indicar que algo aconteceu. Os argumentos que aprovamos tamb√©m s√£o emitidos, facilitando o rastreamento das transfer√™ncias da NFT neste contrato. Os dois √∫ltimos s√£o apenas caminhos de armazenamento para onde estamos armazenando nossa cole√ß√£o NFT. Armazen√°-los no contrato significa que scripts e transa√ß√µes n√£o ter√£o que codific√°-los repetidamente.

  `pub resource NFT: NonFungibleToken.INFT {
    pub let id: UInt64

    pub let name: String
    pub let description: String
    pub let thumbnail: String

    init(
        id: UInt64,
        name: String,
        description: String,
        thumbnail: String,
    ) {
        self.id = id
        self.name = name
        self.description = description
        self.thumbnail = thumbnail
    }
  }`

Nosso recurso NFT evoluiu! Agora ele tem um nome, uma descri√ß√£o e uma miniatura. Estes s√£o para se ajustar √† nova interface - a NFT agora implementa¬†`INFT`¬†do padr√£o NonFungibleToken. Todos esses valores s√£o definidos na cria√ß√£o e s√£o todos¬†`let`, o que significa que voc√™ n√£o pode alterar o nome, o ID ou a miniatura depois!

  `pub resource interface BottomShotCollectionPublic {
    pub fun deposit(token: @NonFungibleToken.NFT)
    pub fun getIDs(): [UInt64]
    pub fun borrowNFT(id: UInt64): &NonFungibleToken.NFT
  }`

Nossa pr√°tica interface de cole√ß√£o p√∫blica tamb√©m adicionou algumas fun√ß√µes extras. Desde que estamos fazendo¬†`deposit`¬†p√∫blico, qualquer pessoa pode adicionar NFTs a esta cole√ß√£o. Se voc√™ n√£o gosta de spam, pode remover¬†`deposit`! Eu vou mant√™-lo, porque quem sabe o que eu posso ter no ar lol

Tamb√©m estamos divulgando publicamente uma fun√ß√£o de "emprestamento. Isto √© da interface, vamos revisar isso quando chegarmos a ela.

`pub resource Collection: BottomShotCollectionPublic, NonFungibleToken.Provider, NonFungibleToken.Receiver, NonFungibleToken.CollectionPublic {`

Nosso principal recurso de coleta muda bastante. Primeiro, implementamos algumas interfaces do padr√£o NonFungibleToken. Voc√™ pode pular no arquivo do contrato para ver o que s√£o.

Em seguida, temos a nova fun√ß√£o de dep√≥sito:

  `pub fun deposit(token: @NonFungibleToken.NFT) {
    let token <- token as! @BottomShot.NFT

    let id: UInt64 = token.id

    // Add the new token to the dictionary which removes the old one
    let oldToken <- self.ownedNFTs[id] <- token

    emit Deposit(id: id, to: self.owner?.address)

    destroy oldToken
  }`

Aqui estamos duas coisas novas: digita√ß√£o ( tipos de convers√£o ) com¬†`as!`¬†e destruindo explicitamente as antigas NFTs. Por que isso √© necess√°rio? Porque Cadence n√£o sabe disso¬†`ownedNFTs[id]`¬†vai ficar vazio. Portanto, obtemos o recurso desse √≠ndice ( mesmo que ele n√£o exista ) e depois movemos o novo.

Nosso¬†`withdraw`¬†fun√ß√£o √© bem semelhante, apenas tem um evento e usa novos tipos.¬†`getIDs`¬†√© id√™ntico.

Finalmente, vemos algo novo:

    `pub fun borrowNFT(id: UInt64): &NonFungibleToken.NFT {
      return (&self.ownedNFTs[id] as &NonFungibleToken.NFT?)!
    }`

Como o nome sugere, essa fun√ß√£o permite que qualquer pessoa pe√ßa emprestada uma NFT de uma cole√ß√£o. Usando-o, buscamos uma refer√™ncia a um objeto em armazenamento sem remov√™-lo do armazenamento. Isso significa que podemos acessar facilmente todos os dados p√∫blicos que o recurso possui, como a descri√ß√£o e a miniatura!

Reparti√ß√£o da sintaxe:

- `pub fun borrowNFT(id: UInt64)UInt64`
    
    : Uma fun√ß√£o p√∫blica chamada emprestnNFT que cont√©m um argumento "id" do tipo
    
- `&NonFungibleToken.NFT`*refer√™ncia*`NonFungibleToken.NFT`
    
    : Retorna um
    
    do tipo
    
- `&self.ownedNFTs[id]`
    
    : Isso obt√©m uma refer√™ncia ao recurso armazenado no √≠ndice [ id ] em NFTs pr√≥prios
    
- `as &NonFungibleToken.NFT?`
    
    : Convertendo tipos
    
- `!`[Force Unwrap](https://docs.onflow.org/cadence/language/values-and-types/#force-unwrap-)
    
    :
    
    o valor em ( ) antes de devolv√™-lo
    

Finalmente, temos a nova fun√ß√£o de hortel√£:

  `pub fun mintNFT(
    recipient: &{NonFungibleToken.CollectionPublic},
    name: String,
    description: String,
    thumbnail: String,
  ) {
    // create a new NFT
    var newNFT <- create NFT(
      id: BottomShot.totalSupply,
      name: name,
      description: description,
      thumbnail: thumbnail
    )

    // deposit it in the recipient's account using their reference
    recipient.deposit(token: <-newNFT)

    BottomShot.totalSupply = BottomShot.totalSupply + UInt64(1)
  }`

Isso √© bem direto. A principal coisa que voc√™ quer prestar aten√ß√£o √© a¬†`thumbnail`¬†campo. Ele armazena a localiza√ß√£o da m√≠dia para nossas NFTs. Pode ser um link HTTP simples ou um hash IPFS.

Agora¬†**isto**¬†√© um contrato que vale a pena implantar!

### üëÄHortel√£ e NFT localmente

Vamos fazer uma transa√ß√£o r√°pida para verificar se nossa fun√ß√£o de hortel√£ funciona. Crie um¬†`mintNFT.cdc`¬†arquive na pasta de transa√ß√µes e adicione o seguinte:

`import BottomShot from 0xf8d6e0586b0a20c7
import NonFungibleToken from 0xf8d6e0586b0a20c7

// Our transaction object now takes in arguments!
transaction(
    recipient: Address,
    name: String,
    description: String,
    thumbnail: String,
) {
  prepare(signer: AuthAccount) {
    // Check if the user sending the transaction has a collection
    if signer.borrow<&BottomShot.Collection>(from: BottomShot.CollectionStoragePath) != nil {
        // If they do, we move on to the execute stage
        return
    }

    // If they don't, we create a new empty collection
    let collection <- BottomShot.createEmptyCollection()

    // Save it to the account
    signer.save(<-collection, to: BottomShot.CollectionStoragePath)

    // Create a public capability for the collection
    signer.link<&{NonFungibleToken.CollectionPublic}>(
        BottomShot.CollectionPublicPath,
        target: BottomShot.CollectionStoragePath
    )
  }

  execute {
    // Borrow the recipient's public NFT collection reference
    let receiver = getAccount(recipient)
        .getCapability(BottomShot.CollectionPublicPath)
        .borrow<&{NonFungibleToken.CollectionPublic}>()
        ?? panic("Could not get receiver reference to the NFT Collection")

    // Mint the NFT and deposit it to the recipient's collection
    BottomShot.mintNFT(
        recipient: receiver,
        name: name,
        description: description,
        thumbnail: thumbnail,
    )
    
    log("Minted an NFT and stored it into the collection")
  } 
}`

Esta √© uma transa√ß√£o mais pesada, repasse os coment√°rios! Faz duas coisas: cria uma cole√ß√£o vazia, se necess√°rio¬†*e*¬†cunha a NFT. Voc√™ deve estar familiarizado com a maior parte da sintaxe aqui. Se algo o confundir, basta marcar @Raza em # section-1-help no Discord e eu irei em seu socorro¬†üòé

N√£o poderemos executar isso com a extens√£o. Digite isso no seu terminal ( para atualizar os argumentos! ):

`flow transactions send ./transactions/mintNFT.cdc "0xf8d6e0586b0a20c7" "CatMoji #1" "Cat emojis on the blockchain" "ipfs://bafybeigmeykxsur4ya2p3nw6r7hz2kp3r2clhvzwiqaashhz5efhewkkgu/0.png"`

Aqui estou criando uma NFT com o nome "CatMoji # 1" e configurando a miniatura para o hash do IPFS. Se voc√™ n√£o estiver usando o IPFS, poder√° substitu√≠-lo pelo seu link regular da Internet, como¬†`https://i.imgur.com/123123.png`.

A sintaxe √©

`flow transactions send <code filename> [<argument> <argument>...] [flags]`

Voc√™ ver√° uma confirma√ß√£o impressa:

[https://camo.githubusercontent.com/bcaac2414f79aab21b88c86fc8eb14ea7291076eb5ee6378fe6df42ee8158129/68747470733a2f2f6861636b6d642e696f2f5f75706c6f6164732f48795a555a735a35632e706e67](https://camo.githubusercontent.com/bcaac2414f79aab21b88c86fc8eb14ea7291076eb5ee6378fe6df42ee8158129/68747470733a2f2f6861636b6d642e696f2f5f75706c6f6164732f48795a555a735a35632e706e67)

**ESTAMOS FAZENDO.**¬†Estamos fazendo NFTs no Flow! Vamos sair do localhost a seguir!

### üö®Relat√≥rio de andamento¬†üö®

Estamos no √∫ltimo trecho.

Poste uma captura de tela dos ativos NFT carregados no IPFS no NFT.storage!